prototype(Neos.DocsNeosIo:Content.Image) < prototype(Neos.Neos:ContentComponent) {
    text = Neos.Neos:Editable {
        property = 'text'
    }
    alternativeText = ${q(node).property('alternativeText')}
    src = Neos.Fusion:Case {
        image {
            condition = ${q(node).property('image') ? true : false}
            renderer = Neos.Neos:ImageUri {
                asset = ${q(node).property('image')}
                maximumWidth = 1920
            }
        }
        dummyImage {
            condition = ${site.context.inBackend}
            renderer = Neos.Fusion:ResourceUri {
                path = 'resource://Neos.Neos/Public/Images/dummy-image.svg'
            }
        }
    }

    renderer = afx`
        <figure class="docs-image">
            <img src={props.src} alt={props.alternativeText} />
            <figcaption @if.has={props.text}>{props.text}</figcaption>
        </figure>
    `
}
