prototype(Neos.DocsNeosIo:Content.PrevNextNavigation) < prototype(Neos.Neos:ContentComponent) {
    prevNode = ${q(documentNode).prev('[instanceof Neos.Neos:Document]').get(0)}
    nextNode = ${q(documentNode).next('[instanceof Neos.Neos:Document]').get(0)}

    renderer = afx`
        <Carbon.Notification:Backend @if.hasNoSibling={!props.prevNode && !props.nextNode}>
            This page currently has no sibling and therefore renders no sibling navigation.
        </Carbon.Notification:Backend>
        <div class="prev-next-navigation row buttons" @if.hasSibling={props.prevNode || props.nextNode}>
            <Carbon.Link:Link node={props.prevNode} additionalClass="btn" renderDefaultTagIfNoLink={false}>
                <Neos.DocsNeosIo:Component.Icon iconClass="fas fa-arrow-left" additionalClass="left inline-icon" />
                <Carbon.Link:Content />
            </Carbon.Link:Link>
            <Carbon.Link:Link node={props.nextNode} additionalClass="btn right" renderDefaultTagIfNoLink={false}>
                <Carbon.Link:Content />
                <Neos.DocsNeosIo:Component.Icon iconClass="fas fa-arrow-right" additionalClass="right inline-icon" />
            </Carbon.Link:Link>
        </div>
    `
}
